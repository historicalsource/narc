	.FILE	'NARCCAD.ASM'
	.TITLE	" <<< N  A  R  C  -- THE CADILLAC >>>"

**************************************************************************
*                                                                        *
* 	COPYRIGHT (C) 1988 WILLIAMS ELECTRONICS GAMES, INC. 		 *
* 	ALL RIGHTS RESERVED.						 *
*                                                                        *
**************************************************************************

	.WIDTH	132
	.OPTION	B,D,L
	.MNOLIST

*	GET THE SYSTEM STUFF
	.INCLUDE	"\video\MPROCEQU.ASM"	;MPROC equates
	.INCLUDE	"\video\DISPEQU.ASM"	;Display processor equates
	.INCLUDE	"\video\GSP.INC"	;GSP assembler equates
	.INCLUDE	"\video\SYS.INC"	;Zunit system equates
	.INCLUDE	"\video\MACROS.HDR"	;Macros, Yeah!
*	LOCAL STUFF
	.INCLUDE	"NARCEQU.ASM"		;NARC Equates
	.INCLUDE	"IMGTBL.GLO"		;Image Label Equates
	.INCLUDE	"NARCFRAG.GLO"
	.INCLUDE	"NARCDRIV.GLO"

	.TEXT
	.INCLUDE	"NARCCAD.TBL"
*
*GLOBALS IN THIS FILE
	.GLOBAL	CFSHRAPB,CFSHRAPA
*
*EXTERNALS
	.GLOBAL	LOFRAG1,LOFRAG2,LOFRAG3,LOFRAG4,LOFDEXP,LOFDEXP1,CNEWPAL
	.GLOBAL	PUT5K,PUT10K

**************************************************************************
*                                                                        *
* CADCOLD - COLD START THE CADILLAC					 *
*                                                                        *
**************************************************************************
CADCOLD
	MOVE	@CADCNT,A0,W
	JRNE	CADCOLDX
	CREATE	CADEPID,CADDY
CADCOLDX
	RETS
**************************************************************************
*                                                                        *
* CADDISP - THE CADILLAC DISPATCHER.					 *
*                                                                        *
**************************************************************************
CADDISP
	SLEEP	0FBH
	MOVE	@CADCNT,A0,W
	CMPI	1,A0
	JRGE	CADDISP
	CREATE	CADEPID,CADDY
	DIE

**************************************************************************
*                                                                        *
* CADDY-THE CADILLAC PROCESS!!!						 *
* ETEMP1 = PTR TO WINDSHIELD OBJECT					 *
* ETEMP2 = PTR TO TRUNK OBJECT						 *
* ETEMP3 = PTR TO FRONT WHEEL OBJECT					 *
* ETEMP4 = PTR TO REAR WHEEL OBJECT					 *
*                                                                        *
**************************************************************************
CADDY
	MOVE	@PSZMIN,A1,W
	MOVE	@PSZMAX,A2,W
	SUB	A1,A2			;A2 = STREET_Z_WIDTH
	JAZ	DUMDIE			;BR = NO STREET TO BE HAD

	CALLA	RANDOM			;GO FOR A RANDOM #
	TEST	A0
	JRN	CADD1A			;BR = START FROM THE LEFT SIDE

	MOVI	CADINIT2,A14
	JRUC	CADD2A

CADD1A
	MOVI	CADINIT1,A14
CADD2A
	MOVI	D1TAB,A6						 
	CALLA	CARINIT			;INITIALIZE THE CAR
	JAZ	DUMDIE			;BR = INITIALIZATION FAILURE
	CALLA	CARSETZ

	MOVK	4,A0			;GIVE THIS GUY SOME SHIT
	CALLA	RANDU
	CALLA	ADDBUCKS
	MOVK	5,A0
	CALLA	RANDU
	CALLA	ADDROCKS

	MOVI	[6,0],A11
	MOVI	1000H,A9
CADMOVE
	CALLA	CXACCEL
	JRNZ	CADMOVE1
	SLOOP	1,CADMOVE
CADMOVE1
*
*DONE WITH THE CREATION, NOW LET'S DO SOMETHING
	MOVE	@CADCNT,A6,W
	INC	A6
	MOVE	A6,@CADCNT,W
	CLR	A11		;A11 = NEXT DECISION COUNTER	 

	MOVI	CARREVR,A0
	CALLA	ONESND
	SLEEP	15H
	MOVI	CARREVR,A0
	CALLA	ONESND
	SLEEP	15H
	MOVI	SQUEELSR,A0
	CALLA	ONESND
*
*WAIT HERE 'TIL THE INITIAL CADDY GETS ON SCREEN
CADONWT
	PUSH	A8
	MOVE	A10,A8
	CALLA	SCRTST		;ONSCREEN?
	MMFM	SP,A8
	JREQ	CADD1   	;BR = YES	 
       	SLOOP	5,CADONWT

CADD1
	SLEEP	4
	MOVE	*A13(EFLAGS),A0,L
	BTST	BDIE,A0
	JRNE	CADIL4		;BR = THE DRIVER IS DEAD

	PUSH	A8
	MOVE	A10,A8
	CALLA	SCRTST		;ONSCREEN?
	MMFM	SP,A8
	JRNE	CCOMEABT   	;BR = NO	 
	JSRP	CADRFIRE
	JRNC	CADIL4
	JRUC	CADD1

*CADDY WENT OFF SCREEN, SPIN HIM AROUND AND GO BACK
CCOMEABT
	CLR	A11
	MOVI	-2000H,A9
	CALLA	CXACCEL

	MOVI	SQUEELSR,A0
	CALLA	ONESND

	MOVE	*A10(OZPOS),A0,W
	MOVE	@PSZMID,A2,W
	CMP	A2,A0
	JRLT	CCOMEAR

	JSRP	CLTURN							 
 	MOVI	-00FF00H,A1						 
	CALLA	CARYZVEL						 
	JSRP	CLEFT180						 
	CLR	A1
	CALLA	CARYZVEL
	JRUC	CCOMEAG
CCOMEAR
	JSRP	CRTURN							 
 	MOVI	00FF00H,A1						 
	CALLA	CARYZVEL						 
	JSRP	CRYTE180						 
	CLR	A1
	CALLA	CARYZVEL
CCOMEAG
	MOVI	CARREVR,A0
	CALLA	ONESND
	SLEEP	44H
	MOVI	[6,0],A11
	MOVI	1000H,A9
	CALLA	CXACCEL
CBACKON
	PUSH	A8
	MOVE	A10,A8
	CALLA	SCRTST		;ON SCREEN
	MMFM	SP,A8
	JREQ	CADD1	   	;BR = YES, TOTALLY	 
	SLOOP	16,CBACKON

*
*CRASH CAR BECAUSE OF DRIVER DEATH
CADIL4
	SOUND1	CVSDOFF
	SLEEP	2
	MOVI	0,A11
	MOVI	-1800H,A9
	CALLA	CXACCEL

	MOVE	*A10(OZPOS),A0,W
	MOVE	@PSZMIN,A1,W
	MOVE	@PSZMAX,A2,W
	SUB	A1,A2			;A2 = STREET_Z_WIDTH
	SRL	1,A2			;A2 = MIDZLINE OF STREET
	ADD	A1,A2
	CMP	A2,A0
	JRLT	CWIPEOR

	MOVI	SPINSNDR,A0
	CALLA	ONESND
	JSRP	CLTURN							 
 	MOVI	0FFFF1000H,A1						 
	CALLA	CARYZVEL						 
	SLEEP	0EH							 
CWIPEOL2								 
	JSRP	CLEFT180						 
	JRUC	CWIPEOG							 
CWIPEOR								 
	MOVI	SPINSNDR,A0
	CALLA	ONESND
	JSRP	CRTURN							 
 	MOVI	00F000H,A1						 
	CALLA	CARYZVEL						 
	MOVI	SPINSNDR,A0
	CALLA	ONESND
	SLEEP	0EH							 
CWIPEOR2								 
	JSRP	CRYTE180						 
CWIPEOG								 
  	CLR	A1
 	CALLA	CARYZVEL
CADDDCL
	MOVE	*A10(OXVEL),A0,L
	JREQ	CADDXST
       	SLOOP	6,CADDDCL
CADDXST
	JSRP	CBOOTY
	MOVI	TRNKSNDR,A0
	CALLA	ONESND
	JSRP	CARTNKO

*
*BLOW UP CADILLAC ON TIME OUT
	MOVI	2*3CH,A0
	CALLA	SRAND
	ADDI	4*3CH,A0
	CALLA	PRCSLP
	PUSHP	A9
	MOVI	18,A9
FLASHLP
	MOVI	PORSHPAL,A1
	CALLA	CNEWPAL
	SLEEP	5
	MOVI	caddy,a1
	CALLA	CNEWPAL
	SLEEP	5
	DSJ	A9,FLASHLP
	PULLP	A9
	MMTM	A12,A8,A9,A10,A11		;CREATE EXPLODE
	MOVE	*A10(ODMAXY),A11,L
	MOVE	*A10(OFLAGS),A9,W
	SLL	16,A9
	MOVE	*A10(OZPOS),A0,W
	MOVX	A0,A9
	MOVE	A10,A8
	MOVI	0BE0H,A10
	CREATE	INDPID,TEXPLODE		;TIME-OUT EXPLOSION
	MMFM	A12,A8,A9,A10,A11
	SOUND1	CVSDOFF
	CALLA	KILLCARP
	CALLA	CDELALL			;DELETE THE COMPANY
	MOVE	A10,A8
	MOVI	BEOID,A9
	CREATE	0EDH,CADEXP		;CREATE THE CADILLAC EXPLODE PROC.
	MOVE	@CADCNT,A6,W
	DEC	A6
	MOVE	A6,@CADCNT,W
	DIE

CADDA1D
	SLEEP	6
	PUSH	A8
	MOVE	A10,A8
	CALLA	SCRTST
	MMFM	SP,A8
	JREQ	CADDA1D   	;BR = STILL ON SCREEN	 
	JRUC	D1DUN

*LET'S MAKE A DECISION						 
									 
	CMPI	0,A11							 
	JRNE	CADDEC			;BR = NO DECISION		 
									 
	CALLA	RANDOM			;RELOAD FOR NEXT DECISION	 
	MOVE	A0,A11							 
	RL	30,A11							 
	SRL	30,A11							 

CADDEC	DEC	A11						 
	JRUC	CADD1							 
									 
D1DUN	SOUND1	CVSDOFF
CADNODRV
	CALLA	KILLCARP
	CALLA	DELCBODY
	CALLA	CDELALL			;DELETE THE COMPANY
	MOVE	@CADCNT,A6,W
	DEC	A6
	MOVE	A6,@CADCNT,W
	DIE

**************************************************************************
*                                                                        *
* CBOOTY - PUT THE BOOTY IN THE TRUNK OF THE CAR.			 *
* 	 DO THIS ONLY AFTER THE CAR HAS STOPPED FOR THE LAST TIME.	 *
* A8 = PTR TO DRIVER							 *
* A10 = PTR TO CAR							 *
* A13 = PROCBLOC							 *
* NOTE: CALL WITH JSRP							 *
*                                                                        *
**************************************************************************
CBOOTY
	MMTM	A12,A9
	MOVB	*A13(EBUCK),A9		;GET THE # OF BUCKS
	JREQ	CBOOTA2
CBOOTA1
	MOVI	[6,28],A3
	CALLA	TRNKBUCK
	SLEEP	1
	DSJS	A9,CBOOTA1
CBOOTA2
	MOVB	*A13(EDRUG),A9		;GET THE # OF DRUGS
	JREQ	CBOOTA4
CBOOTA3
	MOVI	[-7,29],A3
	CALLA	TRNKDRUG
	SLEEP	1
	DSJS	A9,CBOOTA3
CBOOTA4
	MOVB	*A13(EROCK),A9		;GET THE # OF ROCKS
	JREQ	CBOOTA6
CBOOTA5
	MOVI	[8,28],A3
	CALLA	TRNKROCK
	SLEEP	1
	DSJS	A9,CBOOTA5
CBOOTA6
	MOVB	*A13(ECLIP),A9		;GET THE # OF CLIPS
	JREQ	CBOOTA8
CBOOTA7
	MOVI	[8,28],A3
	CALLA	TRNKCLIP
	SLEEP	1
	DSJS	A9,CBOOTA7
CBOOTA8
	MMFM	A12,A9
	RETP

**************************************************************************
*                                                                        *
* CADRFIRE - MAKE THE DRIVER FIRE A RANDOM # OF SHOTS			 *
* A8 = PTR TO DRIVER							 *
* RETURNS:								 *
* CARRY CLEAR = DRIVER DIED IN PROCESS 					 *
*                                                                        *
**************************************************************************
CADRFIRE
	CALLA	RANDOM
	JRNN	CADFIROK		;BR = FIRE AWAY!
	SETC
	RETP
CADFIROK
	MMTM	A12,A9,A11
	MOVE	*A13(ETEMP1),A11,L	;GET THE WINDSHIELD POINTER
	JRZ	CADFIRE1		;BR = NO WINSHEILD
	MOVE	*A11(OZVAL),A0,L
	INC	A0
	MOVE	A0,*A8(OZVAL),L		;GET OUTSIDE DUDE
CADFIRE1
	MOVI	D1DRAW,A9
	JSRP	DRIVERAN		;DRAW THE WEAPON
	JRNC	CADFIREX		;BR = DEATH
	MOVK	3,A0
	CALLA	RANDU
	MOVE	A0,A1
CADD3
	PUSHP	A1
	MOVI	D1FIRE,A9
	JSRP	DRIVERAN
	PULLP	A1
	JRNC	CADFIREX
	MOVK	4,B0
	MOVK	8,B1
	PUSHP	A1
	CALLA	RANGRAND
	SLEEPR	A0
	PULLP	A1
	DSJS	A1,CADD3									 
	MOVI	D1HIDE,A9
	JSRP	DRIVERAN
	JRNC	CADFIREX
	SETC
CADFIREX
	GETST	A9
	TEST	A11
	JRZ	CADFIRZP
	MOVE	*A11(OZVAL),A0,L
	DEC	A0
	MOVE	A0,*A8(OZVAL),L		;GET INSIDE DUDE
CADFIRZP
	PUTST	A9
	MMFM	A12,A9,A11
	RETP

**************************************************************************
*                                                                        *
* DRIVING ENEMY DIE PROCESS						 *
* A8=OBJECT PTR OF ENEMY						 *
* A9=XVEL OF KILLER				 			 *
* A10=YVEL OF KILLER							 *
* A11=OID OF KILLER							 *
* PDATA	    = UPPER LEFT X OF KILLER					 *
* PDATA+10H = UPPER LEFT Y OF KILLER					 *
* PDATA+20H = CURRENT X SIZE OF KILLER					 *
* PDATA+30H = CURRENT Y SIZE OF KILLER					 *
* PDATA+A0H = ADDRESS OF MAIN ANIMATION TABLE				 *
* I.D. = $ED                                                             *
*                                                                        *
**************************************************************************
LDRIVDI
	MOVE	*A8(OPLINK),A14,L
	MOVE	*A14(EFLAGS),A0,L	;GET THE DRIVERS FLAGS
	BTST	BDIE,A0
	JRNE	LDRIVDI1		;BR = THE DRIVER IS ALREADY DEAD	

	ORI	MEDIE,A0
	MOVE	A0,*A14(EFLAGS),L	;MARK THE DRIVER AS DEAD

	MOVE	A11,A3
	SRL	4,A3
	SLL	4,A3
	CMPI	B1OID,A3
	JRNE	LDRIKBY2
	MOVI	P1DATA,A2
	JRUC	LDRISCR
LDRIKBY2
	CMPI	B2OID,A3
	JRNE	LDRIKNK
	MOVI	P2DATA,A2
LDRISCR
	MOVI	10000H,A1
	CALLA	SCORE			;10K FOR SHOOTING DRIVER EXPLOSION
	CALLA	PUT10K
LDRIKNK
	MOVE	A11,A3		  	;COPY OF I.D.
	SLL	28,A3
	SRL	28,A3

	MOVE	*A13(EMAINT),A6,L	;GET THE MAIN ANIMATION TABLE
	MOVK	DFRAME,A0						 
	CALLA	BASFRM							 

	SLL	28,A11			;GET ONLY WEAPON DESCRIPTOR
	SRL	23,A11
	ADD	A11,A6			;AND OFFSET INTO THE ANIMATION TABLE

	MOVE	*A6,A9,L
									 
	MOVK	1,A1
	JSRP	FRANIM			;DO DEATH FRAMES

	SOUND1	CADHORN
LDRIVDI1
DUMPROC	DIE

**************************************************************************
*                                                                        *
* DRIVING ENEMY DEATH BY EXPLOSION					 *
* A8=OBJECT PTR OF ENEMY						 *
* A9=XVEL OF KILLER				 			 *
* A10=YVEL OF KILLER							 *
* A11=OID OF KILLER							 *
* PDATA	    = UPPER LEFT X OF KILLER					 *
* PDATA+10H = UPPER LEFT Y OF KILLER					 *
* PDATA+20H = CURRENT X SIZE OF KILLER					 *
* PDATA+30H = CURRENT Y SIZE OF KILLER					 *
* PDATA+A0H = ADDRESS OF MAIN ANIMATION TABLE				 *
* I.D. = $ED                                                             *
*                                                                        *
**************************************************************************
LDRIVEXP
	MOVE	*A8(OPLINK),A0,L
	MOVE	*A0(EFLAGS),A14,L	;GET THE DRIVERS FLAGS
	ORI	MEDIE,A14
	MOVE	A14,*A0(EFLAGS),L	;MARK THE DRIVER AS DEAD
	CALLA	SCRKIL
	MOVE	A11,A3		  	;COPY OF I.D.
	SLL	28,A3
	SRL	28,A3
	CLR	A5
	MOVB	A5,*A13(EBUCK)		;CLEAR OUT THIS SHIT
	MOVB	A5,*A13(EDRUG)
	MOVB	A5,*A13(EROCK)
	MOVB	A5,*A13(ECLIP)
	JAUC	WEXPD

*INIT TAB FOR PINK CAR PIECES
PINKINIT
	.LONG	0,0,0,0
	.WORD	0,0
	.LONG	CFRAG1,0,DUMCOLL
	.WORD	DMAWNZ,0
	.LONG	0

**************************************************************************
*                                                                        *
* CADILLAC EXPLOSION PROCESS, THIS IS TIMED TO SYNC UP WITH THE CEXPLODE *
*  WHICH RUNS JUST THE FIRE PART OF THE EXPLOSION. THIS PROCESS IS RESPO *
*  FOR DESTROYING THE CAR AND SENDING LITTLE TINY PIECES OF SHRAPNEL ALL *
*  OVER THE PLACE.							 *
* A8 = POINTER TO THE CADILLAC OBJECT					 *
* A9 = OID OF ROCKET							 *
* I.D. = $ED                                                             *
**************************************************************************
CADEXP
	CALLA	PSTOP
	CALLA	BLIPOFF			;TURN THE SCANNER BLIP OFF
	MOVE	A8,A0
	CALLA	DELSHAD
	SRL	4,A9
	SLL	4,A9
	CMPI	B1OID,A9
	JRNE	CADEXP51
	MOVI	P1DATA,A2
	JRUC	CADEXP5K
CADEXP51
	CMPI	B2OID,A9
	JRNE	CADENOK
	MOVI	P2DATA,A2
CADEXP5K
	MOVI	5000H,A1
	CALLA	SCORE			;5K FOR CADILLAC EXPLOSION
	CALLA	PUT5K
CADENOK
	ADDK	EXPLODID,A9		;MAKE THIS SOME SORT OF EXPLOSION
	MOVE	A9,*A8(OID),W		;MAKE THE CAR AN EXPLOSION.

	CALLA	GETOBJ			;GET A BLOCK FOR THE RIGHT SHELL
	JRNE	CADECON			;BR = CREATE O.K., CONTINUE

	MOVE	A8,A0
	CALLA	DELPAL
	CALLA	DELSOBJ			;FAILURE, DELETE AND DIE
	DIE

CADECON
	CALLA	COPYOBJ			;TRANSFER DATA TO THE RIGHT SHELL BLOCK
	MOVE	*A8(OXPOS),A11,W
	MOVE	*A8(OYPOS),A14,W
	SLL	16,A14
	MOVY	A14,A11			;A11 = YPOS:XPOS

	MOVI	CSHELLR,A1
	MOVE	*A0(OFLAGS),A4,W
	MOVE	A8,A10
	MOVE	A0,A8

	CALLA	ANI			;SET-UP SECOND HALF

****	CALLA	OBJOFF			;TURN THIS OBJECT OFF
	CALLA	INSOBJ			;AND INSERT IT
	MOVE	A10,A8
	MOVE	A0,A10			;KEEP HERE FOR SLEEPS

	MOVI	CADEXPFR,A9		;GET THE EXPLOSION FRAMES
	CLR	A1
	JSRP	FRANIM			;FIRST FRAME

	MOVI	JUNKOID,A0		;GET THE GARBAGE OID
	MOVE	A0,*A8(OID),W
	MOVE	A0,*A10(OID),W

****	MOVE	A8,A0
****	CALLA	PULLOBJ

	MOVE	@OVERLOAD,A0
	CMPI	4,A0
	JREQ	FRAGSKIP		;BR = OVERLOAD, SKIP THE FRAGS

* START ALL THE FRAGMENTS HERE

	MMTM	SP,A9,A10,A11
	CALLA	GETCPNT			;GET ENEMY'S CENTER
	MOVY	A1,A11
	MOVX	A1,A11			;SET UP A STARTING POSITION
	MOVE	A11,A5			;SAVE CENTER POSITION 
	MOVI	TRAJEC,A7		;PROCESS FOR FRAGMENTS AND DOOR
	CLR	A1			;ZERO I.D.
	MOVE	*A8(OZPOS),A14,W
	MOVI	DMAWNZ,A0	
	SLL	16,A0
	MOVX	A14,A0			;A0 = [DMA FLAGS,ZPOS]
	MOVE	A0,B5			;CONSTANT THROUGHOUT FRAGMENT CREATION
	MOVI	DBOUNCE,A14		;SAME HERE		
	CLR	B1			;NO 2nd ANIMATION SCRIPT

*SEND FRAGMENTS TO THE RIGHT OF THE EXPLOSION CENTER
	MOVK	6,A6			;THIS MANY PIECES
FRAGR
	CALLA	GETFRAG			;GET FRAGMENT AND IT'S Y VELOCITY
FRAGR3
	MOVI	50,A0 			;RANDOMIZE POSITION & X VELOCITY
	CALLA	RANDU
	ADDXY	A0,A11			;ADD FOR NEW POSITION
	SLL	12,A0
	JREQ	FRAGR3	  		;NO ZEROZ
	MOVE	A0,B4
	CALLA	MAKTPROC
	MOVE	A5,A11			;RESTORE CENTER
	DSJ	A6,FRAGR
*SEND FRAGMENTS TO THE LEFT OF THE EXPLOSION CENTER
	MOVK	6,A6			;THIS MANY PIECES
FRAGL
	CALLA	GETFRAG
FRAGL3
	MOVI	50,A0 			;RANDOMIZE POSITION & VELOCITY
	CALLA	RANDU
     	NEG	A0			;THESE GUYS GO LEFT
	ZEXT	A0			;BLOW OFF THE SIGN
	ADDXY	A0,A11			;ADD FOR NEW POSITION
	SEXT	A0			;RE-SIGN FOR DIVIDE
	ADDXY	A0,A11			;ADD FOR NEW POSITION
	SLL	13,A0
	JREQ	FRAGL3	  		;NO ZEROZ
	MOVE	A0,B4
	CALLA	MAKTPROC
	MOVE	A5,A11			;RESTORE CENTER
	DSJ	A6,FRAGL
*SEND THE DOORS FLYING
DOORFRAG
	MOVI	CDOORA,A9		;FRAMES
	MOVI	PINKINIT,A10
     	MOVK	2,A6
EDOOR
	CALLA	GETYZVEL		;GET THE Y AND Z VELOCITIES
	MOVK	3,A0
	CALLA	RANDU
	BTST	0,A0
	JREQ	EDOOR1
	NEG	A0
EDOOR1
	SLL	16,A0
	MOVE	A0,B4			;XVEL IS LOADED
	CALLA	MAKTPROC
	DSJ	A6,EDOOR
*SEND THE TIRES
CEXPTIRE
	MOVI	CADWEEL1CC,A9		;FRAMES
	MOVI	DTBOUNCE,A14		;DO A TIRE BOUNCY 'TING		
*TIRES TO THE RIGHT OF ME
CEXPTR1
	CALLA	GETYZVEL		;GET THE Y AND Z VELOCITIES
CEXPTR2
	MOVI	50,A0 			;RANDOMIZE POSITION & X VELOCITY
	CALLA	RANDU
	ADDXY	A0,A11			;ADD FOR NEW POSITION
	SLL	12,A0
	JREQ	CEXPTR2	  		;NO ZEROZ
	MOVE	A0,B4			;XVEL IS LOADED
	CALLA	MAKTPROC
	MOVE	A5,A11
*TIRES TO THE LEFT...
CEXPTL3
	CALLA	GETYZVEL		;GET THE Y AND Z VELOCITIES
CEXPTL4
	MOVI	50,A0 			;RANDOMIZE POSITION & VELOCITY
	CALLA	RANDU
     	NEG	A0			;THESE GUYS GO LEFT
	ZEXT	A0			;BLOW OFF THE SIGN
	ADDXY	A0,A11			;ADD FOR NEW POSITION
	SEXT	A0			;RE-SIGN FOR DIVIDE
	ADDXY	A0,A11			;ADD FOR NEW POSITION
	SLL	13,A0
	JREQ	CEXPTL4	  		;NO ZEROZ
	MOVE	A0,B4			;XVEL IS LOADED
	CALLA	MAKTPROC
	MOVE	A5,A11
	MMFM	SP,A9,A10,A11
*CONTINUE WITH THE CADILLAC EXPLOSION
FRAGSKIP
	SLEEP	1

***	MOVE	A10,A0
***	CALLA	OBJON

	CLR	A1
	JSRP	FRANIM			;DO SHELL FRAME

	SLEEP	20

	MOVE	A10,A0
	CALLA	DELSOBJ			;DELETE SHELL RIGHT HALF

	CLR	A1
	JSRP	FRANIM			;STUFF THE WRECKAGE

	JAUC	SINKCARC

**************************************************************************
*                                                                        *
* BLOWSHLD - BLOW OUT THE WINDSHIELD OF THE CADILLAC PROCESS.		 *
* A8 = PTR TO THE WINDSHIELD OBJECT					 *
* A11 = OID OF KILLER							 *
* NOTE: THE BLOWOUT OBJECT MUST BE CREATED, STUFFED, AND ON THE LIST.	 *
*                                                                        *
**************************************************************************
BLOWSHLD
	SOUND1	CADSHLD		;MAKE A BLOWOUT NOISE
	MOVI	CBLOWWS,A9	;LOAD ANIMATION SCRIPT
	SRL	4,A11
	SLL	4,A11
	CMPI	B1OID,A11
	JRNE	BBLOWS1
	MOVI	P1DATA,A2
	JRUC	BBLOWS2
BBLOWS1
	CMPI	B2OID,A11
	JRNE	BLOWAGN
	MOVI	P2DATA,A2
BBLOWS2
	MOVI	250H,A1
	CALLA	SCORE		;250 FOR HITTING THE WINDSHIELD
BLOWAGN
	MOVK	1,A1
	JSRP	FRANIM		;ANIMATE IT
	CLR	A0
	MOVE	A0,*A8(OPLINK),L
	DIE

**************************************************************************
*                                                                        *
* GETFRAG - GETS A RANDOM CAR FRAGMENT					 *
*	    AND ITS Y & Z VELOCITIES, FOR A BEAUTY TRAJECTORY.		 *
* A8 = PTR TO OBLOCK							 *
* RETURN(S)								 *
* A9 = PTR TO FRAGMENT ANIMATION SCRIPT					 *
* A10 = PTR TO CORRECT INIT TABLE FOR MAKTPROC				 *
* B2 = Z VELOCITY (32 BITS)						 *
* B3 = Y VELOCITY (32 BITS)						 *
*                                                                        *
**************************************************************************
GETFRAG
	PUSH	A0
	CALLA	GETYZVEL		;GET A RANDOM Y & Z VELOCITIES

	BTST	0,B3			  			
	JRNE	GETFRAG2		;ODD FRACTION, GIVE ME NORMAL STUFF
	CLR	A10
	TEST	B2			;PIECES ACCORDING TO Z VELOCITY
	JRN	GETFRAG1		;SHRINK WHILE GOING AWAY

	MOVI	CFSHRAPA,A9
	JRUC	GETFRAG4

GETFRAG1
	MOVI	CFSHRAPB,A9
	JRUC	GETFRAG4

GETFRAG2
	MOVI	PINKINIT,A10
	MOVE	B3,A0
	ABS	A0
	SRL	18,A0
	JRZ	GETFRAG3		;LOW VELOCITY PIECE

	MOVI	CSHRAPA,A9	
	JRUC	GETFRAG4

GETFRAG3
	MOVI	CSHRAPB,A9

GETFRAG4
	PULL	A0
	RETS

**************************************************************************
*                                                                        *
* CWINCOLB - CADILLAC WINDSHEILD, COLLISION WITH BOMB			 *
* A0 = PTR TO CADILLAC WINDSHEILD					 *
* A8 = PTR TO ROCKET BOMB						 *
*                                                                        *
**************************************************************************
CWINCOLB
	CALLA	FINDMVP			;GET THE MASTER VEHICLE PROCESS
	CALLA	GETA10			;GET THE PTR TO THE MAIN
	MOVE	A10,A0
	JRUC	CADCOLR
**************************************************************************
*                                                                        *
* CTNKCOLR - CADILLAC TRUNK, COLLISION WITH ROCKET BOMB			 *
* A0 = PTR TO CADILLAC TRUNK						 *
* A8 = PTR TO ROCKET BOMB						 *
*                                                                        *
**************************************************************************
CTNKCOLR
	CALLA	FINDMVP			;GET THE MASTER VEHICLE PROCESS
	CALLA	GETA10			;GET THE PTR TO THE MAIN
	MOVE	A10,A0
	JRUC	CADCOLR
**************************************************************************
*                                                                        *
* CADCOLR - CADILLAC MAIN, COLLISION WITH ROCKET BOMB			 *
* A0 = PTR TO CADILLAC MAIN						 *
* A8 = PTR TO OBJECT THAT HIT IT					 *
*                                                                        *
**************************************************************************
CADCOLR
	MOVI	DUMCOLL,A14
	MOVE	A14,*A0(OCVECT),L	;COLLISION POINTER NULL FOR NOW
	MOVE	*A0(OPLINK),A0,L	;GET THE PROCESS
	MOVE	*A8(OID),A9,W		;GET KILLERS I.D.
	CALLA	GETA8			;GET THE DRIVER PTR
	CALLA	GETA10			;PRESERVE CORRECT STATE FOR CDELALL
	MOVE	A13,B0
	MOVE	A0,A13
	CALLA	CARSTOP
	MOVI	caddy,A1
	CALLA	CNEWPAL
  	CALLA	CDELALL			;DELETE ALL CADILLAC PARTS
	MOVE	B0,A13
	CALLA	KILL
	MOVE	A10,A8			;AND PASS IT TO THE PROCESS
	CREATE	0EDH,CADEXP		;CREATE THE CADILLAC EXPLODE PROC.
	MOVI	CVSDOFF,A0
	CALLA	ONESND			;TURN OFF THE CAR IDLE SOUND
	MOVE	@CADCNT,A0,W
	DEC	A0
	MOVE	A0,@CADCNT,W
CADCOLRX
	CLR	A0
	CLRC
	RETS

*
*DRIVING LOFFREDO COLLISION ROUTINE WITH A BULLET
*A0 = PTR TO LOFFREDO OBJECT
*A8 = PTR TO OBJECT THAT SMASHED HIM
LOFDCOL0
	MOVE	*A8(OXVEL),A9,L		;GET PROJECTILE DATA
	MOVE	*A8(OYVEL),A10,L
	MOVE	*A8(OID),A11,W		

	MOVE	A8,A6			;STORE KILLERS OBLOCK
	MOVE	A0,A8       		;PASS WASTED GUYS POINTER

	MOVE	*A0(OPLINK),A14,L

	CREATE	0EDH,LDRIVDI			;CREATE THE DIE PROCESS
	JREQ	LOFDC0A				;BR = COULDN'T CREATE

	MOVE	*A6(ODMAXY),*A0(PDATA),L	;PASS KILLERS DMA XY POSITION
	MOVE	*A6(OSIZE),*A0(PDATA+20H),L	;AND KILLERS XY SIZE
	MOVE	*A14(EMAINT),*A0(EMAINT),L

LOFDC0A
	CLRC
	CLR	A0
	RETS
*
*DRIVING LOFFREDO COLLISION ROUTINE WITH AN EXPLOSION
*A0 = PTR TO LOFFREDO OBJECT
*A8 = PTR TO OBJECT THAT SMASHED HIM
LOFDCOLE
	MOVE	*A8(OXVEL),A9,L		;GET PROJECTILE DATA
	MOVE	*A8(OYVEL),A10,L
	MOVE	*A8(OID),A11,W		
	MOVI	DUMCOLL,A14
	MOVE	A14,*A0(OCVECT),L
	MOVE	A8,A6			;STORE KILLERS OBLOCK
	MOVE	A0,A8       		;PASS WASTED GUYS POINTER
	MOVE	*A0(OPLINK),A14,L
	CREATE	0EDH,LDRIVEXP			;CREATE THE DIE PROCESS
	JREQ	LOFDCE1				;BR = COULDN'T CREATE
	MOVE	*A6(ODMAXY),*A0(PDATA),L	;PASS KILLERS DMA XY POSITION
	MOVE	*A6(OSIZE),*A0(PDATA+20H),L	;AND KILLERS XY SIZE
	MOVE	*A14(EMAINT),*A0(EMAINT),L
	CLR	A8
	MOVE	A14,A0
	CALLA	PUTA8				;STUFF A ZERO HERE
LOFDCE1
	CLRC
	CLR	A0
	RETS

**************************************************************************
*                                                                        *
* CWINBCOL - VEHICLE WINDSHIELD COLLISION WITH A BULLET			 *
* A0 = PTR TO WINDSHEILD						 *
* A8 = PTR TO THING THAT HIT IT						 *
*                                                                        *
**************************************************************************
CWINBCOL
	MOVE	*A8(OID),A11,W		
	MOVI	DUMCOLL,A14
	MOVE	A14,*A0(OCVECT),L	;SET UP A DUMB COLLISION
	MOVE	A0,A8			;PASS THE BLOWOUT OBJECT
	CREATE	0,BLOWSHLD		;BLOWOUT THE WINDSHIELD
	JREQ	CWINB1			;COULDN'T CREATE THE WINDSHIELD
	MOVE	A0,*A8(OPLINK),L
CWINB1
	CLRC				;DON'T DELETE
	CLR	A0
	RETS

**** DATA SECTION ****
	.data
*
*COLLISION LISTS
CADCOLL
	.WORD	B1OID|ROCKETID
	.LONG	CADCOLR
	.WORD	B2OID|ROCKETID
	.LONG	CADCOLR
	.WORD	B1OID|BOMBID
	.LONG	CADCOLR
	.WORD	B2OID|BOMBID
	.LONG	CADCOLR
	.WORD	BEOID|LANDMOID
	.LONG	CADCOLR
	.WORD	0
	.LONG	DUMRTSG
LOFDCOLL
	.WORD	B1OID|BULLETID
	.LONG	LOFDCOL0
	.WORD	B2OID|BULLETID
	.LONG	LOFDCOL0
	.WORD	B1OID|EXPLODID
	.LONG	LOFDCOLE
	.WORD	B2OID|EXPLODID
	.LONG	LOFDCOLE
	.WORD	0
	.LONG	DUMRTSG
CWIN1COL
	.WORD	B1OID|BULLETID
	.LONG	CWINBCOL
	.WORD	B2OID|BULLETID
	.LONG	CWINBCOL

	.WORD	B1OID|ROCKETID
	.LONG	CWINBCOL
	.WORD	B2OID|ROCKETID
	.LONG	CWINBCOL

	.WORD	B1OID|BOMBID
	.LONG	CWINCOLB
	.WORD	B2OID|BOMBID
	.LONG	CWINCOLB

	.WORD	0
	.LONG	DUMRTSG

CWHL1COL
	.WORD	0
	.LONG	DUMRTSG

CTNK1COL
	.WORD	B1OID|ROCKETID
	.LONG	CTNKCOLR
	.WORD	B2OID|ROCKETID
	.LONG	CTNKCOLR
	.WORD	0
	.LONG	DUMRTSG

CADINIT1
	.LONG	0,0,CADDIXL*10000H,CADDIYL*Y
	.WORD	0,0
	.LONG	CADDY1,CADZ,CADCOLL
	.WORD	DMAWNZ,C1OID|CBODYID
	.LONG	CADBLIP

	.LONG	0,0,CADDIXL*10000H,CADDIYL*Y
	.WORD	0,0
	.LONG	CAD1WIN1,CADZ,CWIN1COL
	.WORD	DMAWNZ,C1OID|WSHLDID
	.LONG	0

	.LONG	0,0,CADDIXL*10000H,CADDIYL*Y
	.WORD	0,0
	.LONG	CAD1TNK1,CADZ,CTNK1COL
	.WORD	DMAWNZ,C1OID|TRUNKID
	.LONG	0

	.LONG	0,0,CADDIXL*10000H,CADDIYL*Y
	.WORD	0,0
	.LONG	CAD1WHL1,CADWHLZ,CWHL1COL
	.WORD	DMAWNZ,C1OID|FTIREID
	.LONG	0

	.LONG	0,0,CADDIXL*10000H,CADDIYL*Y
	.WORD	0,0
	.LONG	CAD1WHL3C,CADWHLZ,CWHL1COL
	.WORD	DMAWNZ,C1OID|RTIREID
	.LONG	0

	.LONG	07FFF7FFFH			;SKIP ETEMP5
	.LONG	07FFF7FFFH			;SKIP ETEMP6
	.LONG	07FFF7FFFH			;SKIP ETEMP7
	.LONG	07FFF7FFFH			;SKIP ETEMP8

	.LONG	0,0,CADDIXL*10000H,CADDIYL*Y
	.WORD	0,0
	.LONG	DRIVER3,LOFDRIVZ,LOFDCOLL
	.WORD	DMAWNZ,C1OID|DRIVERID
	.LONG	0

CADINIT2
	.LONG	0,0,CADDIXR*10000H,CADDIYR*Y
	.WORD	0,0
	.LONG	CADDY1,CADZ,CADCOLL
	.WORD	DMAWNZ|M_FLIPH,C1OID|CBODYID
	.LONG	CADBLIP

	.LONG	0,0,CADDIXR*10000H,CADDIYR*Y
	.WORD	0,0
	.LONG	CAD1WIN1,CADZ,CWIN1COL
	.WORD	DMAWNZ|M_FLIPH,C1OID|WSHLDID
	.LONG	0

	.LONG	0,0,CADDIXR*10000H,CADDIYR*Y
	.WORD	0,0
	.LONG	CAD1TNK1,CADZ,CTNK1COL
	.WORD	DMAWNZ|M_FLIPH,C1OID|TRUNKID
	.LONG	0

	.LONG	0,0,CADDIXR*10000H,CADDIYR*Y
	.WORD	0,0
	.LONG	CAD1WHL1,CADWHLZ,CWHL1COL
	.WORD	DMAWNZ|M_FLIPH,C1OID|FTIREID
	.LONG	0

	.LONG	0,0,CADDIXR*10000H,CADDIYR*Y
	.WORD	0,0
	.LONG	CAD1WHL2C,CADWHLZ,CWHL1COL
	.WORD	DMAWNZ|M_FLIPH,C1OID|RTIREID
	.LONG	0

	.LONG	07FFF7FFFH			;SKIP ETEMP5
	.LONG	07FFF7FFFH			;SKIP ETEMP6
	.LONG	07FFF7FFFH			;SKIP ETEMP7
	.LONG	07FFF7FFFH			;SKIP ETEMP8

	.LONG	0,0,CADDIXR*10000H,CADDIYR*Y
	.WORD	0,0
	.LONG	DRIVER3,LOFDRIVZ,LOFDCOLL
	.WORD	DMAWNZ|M_FLIPH,C1OID|DRIVERID
	.LONG	0

*
D1OFF	.WORD	1,0,2,6,15,15,0,0,0,0,0,0,0,0,0,0,31,32,33,34,35,36,37,38,39
	.WORD	40,41,42,43,44,45,46

*LOFFREDO DRIVING THE CADILLAC ANIMATION PTR TABLE,
*	 FIRST LONG WORD IS THE PTR TO THE FRAME OFFSETS
*
D1TAB	.LONG	D1OFF
	.LONG	D1DRV,D1DETH,D1DETH,LOFDEXP,LOFDEXP1

	.LONG	LOFRAG1,LOFRAG2,LOFRAG2,LOFRAG3
	.LONG	LOFRAG1,LOFRAG2,LOFRAG2,LOFRAG3
	.LONG	LOFRAG4

	.LONG	D1FIRE,D1FIRE,D1FIRE,D1FIRE,D1FIRE,D1FIRE,D1FIRE,D1FIRE
	.LONG	D1FIRE,D1FIRE,D1FIRE,D1FIRE,D1FIRE,D1FIRE,D1FIRE,D1FIRE

	.LONG	CDRIVRYTE,CDRIVLEFT,CADRWHL,CADFWHL,CWHLLEFT,CWHLRYTE	
	.LONG	CADTNKOP,CADTNKCL,CFSPIN,CFSPINF,CRSPIN,CRSPINF
	.LONG	CADRWHLR,CADFWHLR,CWHLLR,CWHLRR

D1DRV				;DRIVER DRIVING ANIMATION
	.LONG	DRIVER1
	.WORD	5
	.LONG	DRIVER2
	.WORD	5
	.LONG	DRIVER3
	.WORD	5
	.LONG	DRIVER4
	.WORD	5
	.LONG	DRIVER5
	.WORD	5
	.LONG	DRIVER4
	.WORD	5
	.LONG	DRIVER3
	.WORD	5
	.LONG	DRIVER2
	.WORD	5
	.LONG	DRIVER1
	.WORD	5
	LONGNULL

CDRIVRYTE			;MAKE THE CADILLAC DRIVER TURN THE CAR RIGHT
	.LONG	DRIVER4
	.WORD	5
	.LONG	DRIVER5
	.WORD	5
	.LONG	0
	.LONG	DRIVER4
	.WORD	5
	.LONG	DRIVER3
	.WORD	5
	.LONG	0

CDRIVLEFT			;MAKE THE CADILLAC DRIVER TURN THE CAR LEFT
	.LONG	DRIVER2
	.WORD	5
	.LONG	DRIVER1
	.WORD	5
	.LONG	0
	.LONG	DRIVER2
	.WORD	5
	.LONG	DRIVER3
	.WORD	5
	.LONG	0

D1DETH
	.LONG	DRVDTH1
	.WORD	6
	.LONG	DRVDTH2
	.WORD	6
	.LONG	DRVDTH3
	.WORD	6
	.LONG	DRVDTH4
	.WORD	6
	.LONG	DRVDTH5
	.WORD	6
	LONGNULL

D1DRAW
	.LONG	DRIVEF1
	.WORD	5
	.LONG	DRIVEF2
	.WORD	6
	.LONG	DRIVEF3
	.WORD	6
	.LONG	DRIVEF4
	.WORD	1
	.LONG	0
D1FIRE
	.LONG	DRIVEF4
	.WORD	SPAWNP|4,26,-4,0EBH
	.LONG	EDBULLET
	.LONG	0
D1HIDE
	.LONG	DRIVEF3
	.WORD	6
	.LONG	DRIVEF2
	.WORD	6
	.LONG	DRIVEF1
	.WORD	6
	.LONG	DRIVER1
	.WORD	1
	LONGNULL

CADBOD		 			;STRAIGHT CADILLAC BODY
	.LONG	CADDY1
	.WORD	1
	.LONG	0
CADWIN					;STRAIGHT CADILLAC WINDSHIELD
	.LONG	CAD1WIN1
	.WORD	1
	.LONG	0
CADEXPFR		 		;CADILLAC EXPLOSION
	.LONG	CSHELLL
	.WORD	12
	.LONG	CSHELLL
	.WORD	8
	.LONG	CWRECK
	.WORD	8
	LONGNULL

CADFWHL				;CADILLAC FRONT WHEEL 1 ROTATE FORWARD
	.LONG	CAD1WHL1
	.WORD	1
	.LONG	CAD1WHL2
	.WORD	1
	.LONG	CAD1WHL3
	.WORD	1
	.LONG	CAD1WHL4
	.WORD	1
	.LONG	0

CADRWHL				;CADILLAC REAR WHEEL 1 ROTATE FORWARD
	.LONG	CAD1WHL1C
	.WORD	1
	.LONG	CAD1WHL2C
	.WORD	1
	.LONG	CAD1WHL3C
	.WORD	1
	.LONG	CAD1WHL4C
	.WORD	1
	.LONG	0

CADFWHLR			;CADILLAC FRONT WHEEL 1 ROTATE REVERSE
	.LONG	CAD1WHL4
	.WORD	1
	.LONG	CAD1WHL3
	.WORD	1
	.LONG	CAD1WHL2
	.WORD	1
	.LONG	CAD1WHL1
	.WORD	1
	.LONG	0

CADRWHLR			;CADILLAC REAR WHEEL 1 ROTATE REVERSE
	.LONG	CAD1WHL4C
	.WORD	1
	.LONG	CAD1WHL3C
	.WORD	1
	.LONG	CAD1WHL2C
	.WORD	1
	.LONG	CAD1WHL1C
	.WORD	1
	.LONG	0

CADWEEL1CC			;CADILLAC WHEEL W/CENTERED ANIMATION POINT
	.LONG	CAD1WHL1CC
	.WORD	1
	.LONG	CAD1WHL2CC
	.WORD	1
	.LONG	CAD1WHL3CC
	.WORD	1
	.LONG	CAD1WHL4CC
	.WORD	1
	.LONG	0

CWHLRYTE			;CADILLAC WHEELS TURN RYTE
	.LONG	CRYTEWHL1
	.WORD	1
	.LONG	CRYTEWHL2
	.WORD	1
	.LONG	CRYTEWHL3
	.WORD	1
	.LONG	CRYTEWHL4
	.WORD	1
	.LONG	0

CWHLRR				;CADILLAC WHEELS TURN RYTE REVERSE
	.LONG	CRYTEWHL4
	.WORD	1
	.LONG	CRYTEWHL3
	.WORD	1
	.LONG	CRYTEWHL2
	.WORD	1
	.LONG	CRYTEWHL1
	.WORD	1
	.LONG	0

CWHLLEFT			;CADILLAC FRONT WHEELS LEFT
	.LONG	CLEFTWHL1
	.WORD	1
	.LONG	CLEFTWHL2
	.WORD	1
	.LONG	CLEFTWHL3
	.WORD	1
	.LONG	CLEFTWHL4
	.WORD	1
	.LONG	0

CWHLLR				;CADILLAC FRONT WHEELS LEFT REVERSE
	.LONG	CLEFTWHL4
	.WORD	1
	.LONG	CLEFTWHL3
	.WORD	1
	.LONG	CLEFTWHL2
	.WORD	1
	.LONG	CLEFTWHL1
	.WORD	1
	.LONG	0

CADTNKOP			;CADILLAC TRUNK OPEN
	.LONG	CAD1TNK1
	.WORD	6
	.LONG	CAD1TNK2
	.WORD	6
	.LONG	CAD1TNK3
	.WORD	6
	.LONG	CAD1TNK4
	.WORD	6
	.LONG	0

CADTNKCL			;CADILLAC TRUNK CLOSE
	.LONG	CAD1TNK4
	.WORD	6
	.LONG	CAD1TNK3
	.WORD	6
	.LONG	CAD1TNK2
	.WORD	6
	.LONG	CAD1TNK1
	.WORD	6
	.LONG	0

CBLOWWS		 		;CADILLAC WINDSHIELD BLOWOUT
	.LONG	CADSHAT1
	.WORD	7
	.LONG	CADSHAT2
	.WORD	6
	.LONG	CADSHAT3
	.WORD	6
	.LONG	CADSHAT4
	.WORD	5
	.LONG	CADSHAT5
	.WORD	4
	LONGNULL

CFSPIN				;CADILLAC FORWARD 180 UNFLIPPED	
	.LONG	CADFORS60
	.WORD	7
	.LONG	CADFORS90
	.WORD	6
	.LONG	CADFORS60
	.WORD	FLIPBITS|7,M_FLIPH
	.LONG	CADDY1
	.WORD	2
	.LONG	0

CFSPINF				;CADDY FORWARD 180, FLIPPED	
	.LONG	CADFORS60
	.WORD	7
	.LONG	CADFORS90
	.WORD	6
	.LONG	CADFORS60
	.WORD	FLIPBITS|7,0
	.LONG	CADDY1
	.WORD	2
	.LONG	0

CRSPIN				;CADDY REVERSE 180 UNFLIPPED	
	.LONG	CADREVS60
	.WORD	7
	.LONG	CADREVS90
	.WORD	6
	.LONG	CADREVS60
	.WORD	FLIPBITS|7,M_FLIPH
	.LONG	CADDY1
	.WORD	2
	.LONG	0

CRSPINF		 		;CADILLAC REVERSE 180, FLIPPED
	.LONG	CADREVS60
	.WORD	7
	.LONG	CADREVS90
	.WORD	6
	.LONG	CADREVS60
	.WORD	FLIPBITS|7,0
	.LONG	CADDY1
	.WORD	2
	.LONG	0

CFSHRAPA	 		;CADILLAC SHRAPNEL ANIMATION
	.LONG	CSHRAPC1
	.WORD	5
	.LONG	CSHRAPC2
	.WORD	5
	.LONG	CSHRAPC3
	.WORD	5
	.LONG	CSHRAPC4
	.WORD	5
	.LONG	CSHRAPC5
	.WORD	5

	.LONG	CSHRAPC1
	.WORD	5
	.LONG	CSHRAPC2
	.WORD	5
	.LONG	CSHRAPC3
	.WORD	5
	.LONG	CSHRAPC4
	.WORD	5
	.LONG	CSHRAPC5
	.WORD	5

	.LONG	CSHRAPC1
	.WORD	5
	.LONG	CSHRAPC2
	.WORD	5
	.LONG	CSHRAPC3
	.WORD	5
	.LONG	CSHRAPC4
	.WORD	5
	.LONG	CSHRAPC5
	.WORD	5

	.LONG	CSHRAPD1
	.WORD	5
	.LONG	CSHRAPD2
	.WORD	5
	.LONG	CSHRAPD3
	.WORD	5
	.LONG	CSHRAPD4
	.WORD	5
	.LONG	CSHRAPD5
	.WORD	5

	.LONG	CSHRAPE1
	.WORD	5
	.LONG	CSHRAPE2
	.WORD	5
	.LONG	CSHRAPE3
	.WORD	5
	.LONG	CSHRAPE4
	.WORD	5
	.LONG	CSHRAPE5
	.WORD	5

	.LONG	CSHRAPE1
	.WORD	5
	.LONG	CSHRAPE2
	.WORD	5
	.LONG	CSHRAPE3
	.WORD	5
	.LONG	CSHRAPE4
	.WORD	5
	.LONG	CSHRAPE5
	.WORD	5

	.LONG	CSHRAPE1
	.WORD	5
	.LONG	CSHRAPE2
	.WORD	5
	.LONG	CSHRAPE3
	.WORD	5
	.LONG	CSHRAPE4
	.WORD	5
	.LONG	CSHRAPE5
	.WORD	5

	.LONG	CSHRAPE1
	.WORD	5
	.LONG	CSHRAPE2
	.WORD	5
	.LONG	CSHRAPE3
	.WORD	5
	.LONG	CSHRAPE4
	.WORD	5
	.LONG	CSHRAPE5
	.WORD	5

	.LONG	CSHRAPE1
	.WORD	5
	.LONG	CSHRAPE2
	.WORD	5
	.LONG	CSHRAPE3
	.WORD	5
	.LONG	CSHRAPE4
	.WORD	5
	.LONG	CSHRAPE5
	.WORD	5

	.LONG	CSHRAPE1
	.WORD	5
	.LONG	CSHRAPE2
	.WORD	5
	.LONG	CSHRAPE3
	.WORD	5
	.LONG	CSHRAPE4
	.WORD	5
	.LONG	CSHRAPE5
	.WORD	5

	.LONG	CSHRAPE1
	.WORD	5
	.LONG	CSHRAPE2
	.WORD	5
	.LONG	CSHRAPE3
	.WORD	5
	.LONG	CSHRAPE4
	.WORD	5
	.LONG	CSHRAPE5
	.WORD	5

	.LONG	CSHRAPE1
	.WORD	5
	.LONG	CSHRAPE2
	.WORD	5
	.LONG	CSHRAPE3
	.WORD	5
	.LONG	CSHRAPE4
	.WORD	5
	.LONG	CSHRAPE5
	.WORD	5

	.LONG	CSHRAPE1
	.WORD	5
	.LONG	CSHRAPE2
	.WORD	5
	.LONG	CSHRAPE3
	.WORD	5
	.LONG	CSHRAPE4
	.WORD	5
	.LONG	CSHRAPE5
	.WORD	5

	.LONG	CSHRAPE1
	.WORD	5
	.LONG	CSHRAPE2
	.WORD	5
	.LONG	CSHRAPE3
	.WORD	5
	.LONG	CSHRAPE4
	.WORD	5
	.LONG	CSHRAPE5
	.WORD	5

	.LONG	CSHRAPE1
	.WORD	5
	.LONG	CSHRAPE2
	.WORD	5
	.LONG	CSHRAPE3
	.WORD	5
	.LONG	CSHRAPE4
	.WORD	5
	.LONG	CSHRAPE5
	.WORD	5

	.LONG	CSHRAPE1
	.WORD	5
	.LONG	CSHRAPE2
	.WORD	5
	.LONG	CSHRAPE3
	.WORD	5
	.LONG	CSHRAPE4
	.WORD	5
	.LONG	CSHRAPE5
	.WORD	5

	.LONG	CSHRAPE1
	.WORD	5
	.LONG	CSHRAPE2
	.WORD	5
	.LONG	CSHRAPE3
	.WORD	5
	.LONG	CSHRAPE4
	.WORD	5
	.LONG	CSHRAPE5
	.WORD	5

	.LONG	CSHRAPE1
	.WORD	5
	.LONG	CSHRAPE2
	.WORD	5
	.LONG	CSHRAPE3
	.WORD	5
	.LONG	CSHRAPE4
	.WORD	5
	.LONG	CSHRAPE5
	.WORD	5

	.LONG	CSHRAPE1
	.WORD	5
	.LONG	CSHRAPE2
	.WORD	5
	.LONG	CSHRAPE3
	.WORD	5
	.LONG	CSHRAPE4
	.WORD	5
	.LONG	CSHRAPE5
	.WORD	5

	LONGNULL

CFSHRAPB				;CADILLAC FRAGMENT ANIMATION
	.LONG	CSHRAPE1
	.WORD	5
	.LONG	CSHRAPE2
	.WORD	5
	.LONG	CSHRAPE3
	.WORD	5
	.LONG	CSHRAPE4
	.WORD	5
	.LONG	CSHRAPE5
	.WORD	5

	.LONG	CSHRAPE1
	.WORD	5
	.LONG	CSHRAPE2
	.WORD	5
	.LONG	CSHRAPE3
	.WORD	5
	.LONG	CSHRAPE4
	.WORD	5
	.LONG	CSHRAPE5
	.WORD	5

	.LONG	CSHRAPE1
	.WORD	5
	.LONG	CSHRAPE2
	.WORD	5
	.LONG	CSHRAPE3
	.WORD	5
	.LONG	CSHRAPE4
	.WORD	5
	.LONG	CSHRAPE5
	.WORD	5

	.LONG	CSHRAPE1
	.WORD	5
	.LONG	CSHRAPE2
	.WORD	5
	.LONG	CSHRAPE3
	.WORD	5
	.LONG	CSHRAPE4
	.WORD	5
	.LONG	CSHRAPE5
	.WORD	5

	.LONG	CSHRAPE1
	.WORD	5
	.LONG	CSHRAPE2
	.WORD	5
	.LONG	CSHRAPE3
	.WORD	5
	.LONG	CSHRAPE4
	.WORD	5
	.LONG	CSHRAPE5
	.WORD	5

	.LONG	CSHRAPE1
	.WORD	5
	.LONG	CSHRAPE2
	.WORD	5
	.LONG	CSHRAPE3
	.WORD	5
	.LONG	CSHRAPE4
	.WORD	5
	.LONG	CSHRAPE5
	.WORD	5

	.LONG	CSHRAPD1
	.WORD	5
	.LONG	CSHRAPD2
	.WORD	5
	.LONG	CSHRAPD3
	.WORD	5
	.LONG	CSHRAPD4
	.WORD	5
	.LONG	CSHRAPD5
	.WORD	5

	.LONG	CSHRAPD1
	.WORD	5
	.LONG	CSHRAPD2
	.WORD	5
	.LONG	CSHRAPD3
	.WORD	5
	.LONG	CSHRAPD4
	.WORD	5
	.LONG	CSHRAPD5
	.WORD	5

	.LONG	CSHRAPC1
	.WORD	5
	.LONG	CSHRAPC2
	.WORD	5
	.LONG	CSHRAPC3
	.WORD	5
	.LONG	CSHRAPC4
	.WORD	5
	.LONG	CSHRAPC5
	.WORD	5

	.LONG	CSHRAPC1
	.WORD	5
	.LONG	CSHRAPC2
	.WORD	5
	.LONG	CSHRAPC3
	.WORD	5
	.LONG	CSHRAPC4
	.WORD	5
	.LONG	CSHRAPC5
	.WORD	5

	.LONG	CSHRAPC1
	.WORD	5
	.LONG	CSHRAPC2
	.WORD	5
	.LONG	CSHRAPC3
	.WORD	5
	.LONG	CSHRAPC4
	.WORD	5
	.LONG	CSHRAPC5
	.WORD	5

	.LONG	CSHRAPC1
	.WORD	5
	.LONG	CSHRAPC2
	.WORD	5
	.LONG	CSHRAPC3
	.WORD	5
	.LONG	CSHRAPC4
	.WORD	5
	.LONG	CSHRAPC5
	.WORD	5

	.LONG	CSHRAPC1
	.WORD	5
	.LONG	CSHRAPC2
	.WORD	5
	.LONG	CSHRAPC3
	.WORD	5
	.LONG	CSHRAPC4
	.WORD	5
	.LONG	CSHRAPC5
	.WORD	5

	.LONG	CSHRAPC1
	.WORD	5
	.LONG	CSHRAPC2
	.WORD	5
	.LONG	CSHRAPC3
	.WORD	5
	.LONG	CSHRAPC4
	.WORD	5
	.LONG	CSHRAPC5
	.WORD	5

	.LONG	CSHRAPC1
	.WORD	5
	.LONG	CSHRAPC2
	.WORD	5
	.LONG	CSHRAPC3
	.WORD	5
	.LONG	CSHRAPC4
	.WORD	5
	.LONG	CSHRAPC5
	.WORD	5

	.LONG	CSHRAPC1
	.WORD	5
	.LONG	CSHRAPC2
	.WORD	5
	.LONG	CSHRAPC3
	.WORD	5
	.LONG	CSHRAPC4
	.WORD	5
	.LONG	CSHRAPC5
	.WORD	5

	.LONG	CSHRAPC1
	.WORD	5
	.LONG	CSHRAPC2
	.WORD	5
	.LONG	CSHRAPC3
	.WORD	5
	.LONG	CSHRAPC4
	.WORD	5
	.LONG	CSHRAPC5
	.WORD	5

	.LONG	CSHRAPC1
	.WORD	5
	.LONG	CSHRAPC2
	.WORD	5
	.LONG	CSHRAPC3
	.WORD	5
	.LONG	CSHRAPC4
	.WORD	5
	.LONG	CSHRAPC5
	.WORD	5

	LONGNULL

CDOORA				;SPINNING CAR DOOR ANIMATION
	.LONG	CDOOR1
	.WORD	5
	.LONG	CDOOR2
	.WORD	5
	.LONG	CDOOR3
	.WORD	5
	.LONG	CDOOR4
	.WORD	5
	.LONG	CDOOR5
	.WORD	5
	.LONG	CDOOR1
	.WORD	5
	.LONG	CDOOR2
	.WORD	5
	.LONG	CDOOR3
	.WORD	5
	.LONG	CDOOR4
	.WORD	5
	.LONG	CDOOR5
	.WORD	5
	LONGNULL

CSHRAPA					;CADILLAC SHRAPNEL ANIMATION
	.LONG	CSHRAP1
	.WORD	5
	.LONG	CSHRAP2
	.WORD	5
	.LONG	CSHRAP3
	.WORD	5
	.LONG	CSHRAP4
	.WORD	5
	.LONG	CSHRAP5
	.WORD	5
	.LONG	CSHRAP1
	.WORD	5
	.LONG	CSHRAP2
	.WORD	5
	.LONG	CSHRAP3
	.WORD	5
	.LONG	CSHRAP4
	.WORD	5
	.LONG	CSHRAP5
	.WORD	5
	.LONG	CSHRAP1
	.WORD	5
	.LONG	CSHRAP2
	.WORD	5
	.LONG	CSHRAP3
	.WORD	5
	.LONG	CSHRAP4
	.WORD	5
	.LONG	CSHRAP5
	.WORD	5
	LONGNULL

CSHRAPB					;CADILLAC FRAGMENT ANIMATION
	.LONG	CFRAG1
	.WORD	5
	.LONG	CFRAG2
	.WORD	5
	.LONG	CFRAG3
	.WORD	5
	.LONG	CFRAG4
	.WORD	5
	.LONG	CFRAG5
	.WORD	5
	.LONG	CFRAG1
	.WORD	5
	.LONG	CFRAG2
	.WORD	5
	.LONG	CFRAG3
	.WORD	5
	.LONG	CFRAG4
	.WORD	5
	.LONG	CFRAG5
	.WORD	5
	.LONG	CFRAG1
	.WORD	5
	.LONG	CFRAG2
	.WORD	5
	.LONG	CFRAG3
	.WORD	5
	.LONG	CFRAG4
	.WORD	5
	.LONG	CFRAG5
	.WORD	5
	LONGNULL

	.END
