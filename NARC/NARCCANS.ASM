	.FILE	'NARCCANS.ASM'
	.TITLE	" <<< N  A  R  C  -- CANS, DUMPSTERS & THE LIKE >>>"

**************************************************************************
*                                                                        *
* 	COPYRIGHT (C) 1988 WILLIAMS ELECTRONICS GAMES, INC. 		 *
* 	ALL RIGHTS RESERVED.						 *
*                                                                        *
**************************************************************************

	.OPTION	B,D,L
	.MNOLIST

*	GET THE SYSTEM STUFF
	.INCLUDE	"\video\MPROCEQU.ASM"	;MPROC equates
	.INCLUDE	"\video\DISPEQU.ASM"	;Display processor equates
	.INCLUDE	"\video\GSP.INC"	;GSP assembler equates
	.INCLUDE	"\video\SYS.INC"	;Zunit system equates
	.INCLUDE	"\video\MACROS.HDR"	;Macros, Yeah!
*	LOCAL STUFF
	.INCLUDE	"NARCEQU.ASM"		;NARC Equates
	.INCLUDE	"IMGTBL.GLO"		;Image Label Equates

*
*GLOBALS IN THIS FILE
	.GLOBAL	CANEXCOL

	.TEXT
**************************************************************************
*                                                                        *
* CANAWAY - MAKE A BACK/FOREGROUND OBJECT GET HURLED FROM AN EXPLOSION	 *
* A8 = PTR TO BACK/FOREGROUND OBJECT					 *
* A10 = PTR TO THE 'TING THAT SMASHED IT MAAHN				 *
*                                                                        *
**************************************************************************
*
*SEND CAN BY WAY OF CAR
CANCAWAY
	CALLA	GCUBEVEL
	MOVE	A3,B2			;MOVE IN Z VEL FOR CANGAWAY
	MOVE	A2,B3			;MOVE IN Y VEL FOR CANGAWAY
	OR	A1,A2
	JRNZ	CANCAW00		;THERE BE VELOCITY
	MOVE	*A8(OXVEL),A0,L
	JRNZ	CANCAW00
	MOVE	*A8(OYVEL),A0,L
	JRNZ	CANCAW00
	CLR	A0
	MOVE	A0,*A8(OPLINK),L
	DIE
CANCAW00
	MOVE	*A8(OCVECT),A11,L
	MOVI	DUMCOLL,A14
	MOVE	A14,*A8(OCVECT),L	;CANCEL UPWARD COLLISIONS
	MOVE	A1,A4
	ABS	A4
	MOVE	@SCROLLX,A5,L
	ABS	A5
	SUB	A5,A4
	ABS	A4	
	SRL	16,A4
	CMPI	3,A4
	JRHI	CANCAW0
	SOUND1	CANWPSND
	JRUC	CANCAW01
CANCAW0	
	SOUND1	CANSMSND
CANCAW01
	MOVE	A1,*A8(OXVEL),L		;STUFF X VEL
	MOVE	*A10(OPLINK),A0,L	
	JRZ	CANGAWAY		;BR = NO CAR CONTROL PROCESS, HMMM
	MOVE	A13,B0
	MOVE	A8,B1
	CALLA	GETA8
	MOVE	A0,A13
	MOVE	*A10(OXVEL),A1,L	;LET'S STEAL SOME CAR VELOCITY
	JRN	CANCAW3			;BR = NEGATIVE, ADD TO SUBTRACT
	SUBI	[0,4000H],A1
	JRNN	CANCAW3A		;BR = SIGNS, DIDN'T SWITCH
	JRUC	CANCAW3B
CANCAW3
	ADDI	[0,4000H],A1
	JRN	CANCAW3A
CANCAW3B
	CLR	A1
CANCAW3A
	CALLA	CARXVEL			;GIVE ME YOU'RE WORST!
	MOVE	B0,A13
	MOVE	B1,A8
CANCAW4
	JRUC	CANGAWAY

*
*MOVE CAN OR DUMPSTER BY WAY OF PLAYER START CAR
CANSAWAY
	CALLA	GCUBEVEL
	MOVE	A3,B2			;MOVE IN Z VEL FOR CANGAWAY
	MOVE	A2,B3			;MOVE IN Y VEL FOR CANGAWAY
	OR	A1,A2
	JRNZ	CANSAW00		;THERE BE VELOCITY
	MOVE	*A8(OXVEL),A0,L
	JRNZ	CANSAW00
	MOVE	*A8(OYVEL),A0,L
	JRNZ	CANSAW00
	CLR	A0
	MOVE	A0,*A8(OPLINK),L
	DIE
CANSAW00
	MOVE	*A8(OCVECT),A11,L
	MOVI	DUMCOLL,A14
	MOVE	A14,*A8(OCVECT),L	;CANCEL UPWARD COLLISIONS
	MOVE	A1,A4
	ABS	A4
	MOVE	@SCROLLX,A5,L
	ABS	A5
	SUB	A5,A4
	ABS	A4	
	SRL	16,A4
	CMPI	3,A4
	JRHI	CANSAW0
	SOUND1	CANWPSND
	JRUC	CANSAW01
CANSAW0	
	SOUND1	CANSMSND
CANSAW01
	MOVE	A1,*A8(OXVEL),L		;STUFF X VEL
	JRUC	CANGAWAY

*
*MOVE DUMPSTER VIA CAR
DMPCAWAY
	CALLA	GCUBEVEL
	SRA	1,A2
	SRA	1,A3
	MOVE	A3,B2			;MOVE IN Z VEL FOR CANGAWAY
	MOVE	A2,B3			;MOVE IN Y VEL FOR CANGAWAY
	OR	A1,A2
	JRNZ	DMPCAW00		;THERE BE VELOCITY
	MOVE	*A8(OXVEL),A0,L
	JRNZ	DMPCAW00
	MOVE	*A8(OYVEL),A0,L
	JRNZ	DMPCAW00
	CLR	A0
	MOVE	A0,*A8(OPLINK),L
	DIE
DMPCAW00
	MOVE	*A8(OCVECT),A11,L
	MOVI	DUMCOLL,A14
	MOVE	A14,*A8(OCVECT),L	;CANCEL UPWARD COLLISIONS
	SOUND1	CANSMSND
	MOVE	A1,*A8(OXVEL),L		;STUFF X VEL
	MOVE	A13,B0
	MOVE	*A10(OPLINK),A13,L	
	JRZ	DMPCAW03		;BR = NO CAR CONTROL PROCESS, HMMM
	MOVE	A10,A0
	MOVE	A1,A1
	JRN	DMPCAW01
	CALLA	WHICHSID
	JRC	DMPCAW03		;BR = GOING RIGHT ON THE RIGHT, CAN IT
	JRUC	DMPCAW02
DMPCAW01
	CALLA	WHICHSID
	JRNC	DMPCAW03		;BR = GOING LEFT ON THE LEFT, CAN IT
DMPCAW02
	NEG	A1				
	SRA	1,A1
	MOVE	*A0(OZVEL),A2,L
	MOVE	*A0(OYVEL),A3,L
	SUB	A2,A3
	NEG	A2
	SRA	1,A2
	ADD	A2,A3
	MOVE	A8,B1
	MOVE	A13,A0
	CALLA	GETA8			;GET THE DRIVER PTR
	CALLA	CARXVEL			;MIRROR/2 X VELOCITY
	MOVE	A2,A1
	CALLA	CARZVEL			;MIRROR/2 Z VELOCITY
	MOVE	A3,A1
	CALLA	CARYVEL			;SUBSEQUENT Y VELOCITY
	MOVE	B1,A8
DMPCAW03
	MOVE	B0,A13
	JRUC	CANGAWAY

*
*SEND CAN BY WAY OF ROCKET
CANRAWAY
	MOVE	*A8(OCVECT),A11,L
	MOVI	DUMCOLL,A14
	MOVE	A14,*A8(OCVECT),L	;CANCEL UPWARD COLLISIONS
	MOVI	17FFFH,B0
	MOVI	40000H,B1
	CALLA	RANGRAND
	MOVE	A0,A6
	MOVE	*A10(OFLAGS),A0,W
	BTST	B_FLIPH,A0
	JREQ	CANRGAWA
	NEG	A6
	JRUC	CANRGAWA
CANAWAY
	CLR	A6
	MOVE	*A8(OCVECT),A11,L
*
*GENERIC CAN FLY ENTRY, A6 = 32 BIT AMOUNT OF X VEL TO ADD,A11 = COLVECT RESTORE 
CANRGAWA
	MOVE	*A10(OXPOS),A0,W
	MOVE	*A10(OYPOS),A1,W
	SLL	16,A1
	MOVY	A1,A0
	MOVE	*A10(OSIZE),A1,L	;GET DMA XY SIZE OF KILLER
	CALLA	GETCENT
	MOVE	A1,A2			;KEEP CENTER OF KILLER
	CALLA	GETCPNT			;GET ENEMY'S CENTER

	SEXT	A1			;JUST DEAL WITH X CENTER
	SUB	A2,A1			;A1 = ENEMY XCENT - KILLER XCENT
	MOVE	A1,A2
	SLL	11,A2
	ADD	A6,A2
	MOVE	A2,*A8(OXVEL),L		;MAKE DCENTERS A VELOCITY
	CALLA	GETYZVEL		;GET A RANDOM Y & Z VELOCITY
*
*ENTRY FOR CAN FLY WITH X VELOCITY ALREADY STUFFED
*B2 = 32 BIT ZVEL, B3 = 32 BIT YVEL, A11 = COLLISION VECTOR TO RESTORE
CANGAWAY
	MOVE	A8,A0
	CALLA	ISBB
	JRNC	CANOFFBB
	CALLA	DELBB
CANOFFBB
	MOVE	*A8(OSHAD),A0,L
	JRNZ	CANSHAD
	CALLA	SHADST			;START A SHADOW
CANSHAD	
	MOVE	A8,B0
	MOVE	B2,*B0(OZVEL),L
	MOVE	*B0(OYVEL),B1,L		;GET THE CURRENT Y VELOCITY
	JRLE	CANRST
	ADD	B1,B3			;FREAK THE CAN OUT
	JRGT	CANRST
	MOVI	[1,0],B3
CANRST
	ADD	B2,B3
	MOVE	B3,*B0(OYVEL),L		;STUFF THE ILL GOTTEN BOOTY
	CALLA	SHVELCPY
CANPDS
	SLEEP	1
	CALLA	CKZAMIN			;JUST CHECK AGAINST THE MIN FOR WALL
***	CALLA 	CKZADJST
	MOVE	*A8(OYVEL),A0,L		;GET THE CURRENT Y VELOCITY
	MOVE	*A8(OZVEL),A1,L	
	SUB	A1,A0			;SUBTRACT INSTALLED Z VELOCITY
	JRLE	CANPD1B			;BR = NOT GOING DOWN, DON'T CHECK GROUND HIT
	MOVE	A11,*A8(OCVECT),L	;RESTORE ORIGINAL COLLISION VECTOR
	JRUC	CANDOWN
CANPD1B
	ADDI	GRAVITY,A0
	ADD	A1,A0			;RE-ADD THE Z VEL
	MOVE	A0,*A8(OYVEL),L
	JRUC	CANPDS
CANDOWN
	CALLA	DFRMGRND		;DID HE HIT THE GROUND?
	JRLE	CANHIT			;BR = YES
	MOVE	*A8(OYVEL),A0,L		;DECREASE Y VELOCITY
	MOVE	*A8(OZVEL),A1,L
	SUB	A1,A0
	ADDI	GRAVITY,A0
	ADD	A1,A0
	MOVE	A0,*A8(OYVEL),L
	SLEEP	1
	CALLA	CKZAMIN
***	CALLA	CKZADJST
	JRUC	CANDOWN

CANHIT
	CALLA	SCRTST
	JRNZ	CANHITNS		;BR = NO SOUND IF OFF SCREEN
	MOVE	*A8(OID),A0,W
	CMPI	BGDMPID,A0
	JRNE	CANHITSD
	SOUND1	CANSMSND		;BIG SOUND FOR DUMPSTER
	JRUC	CANHITNS
CANHITSD
	SOUND1	CANBCSND
CANHITNS
	MOVE	*A8(OYVEL),A0,L
	SRA	2,A0
	CMPI	[1,0],A0
	JRLT	CANHIT1
	NEG	A0
	MOVE	A0,*A8(OYVEL),L
	MOVE	*A8(OXVEL),A1,L
	SRA	1,A1	       		;HALVE THESE GUYS
	MOVE	A1,*A8(OXVEL),L
	MOVE	*A8(OZVEL),A1,L
	SRA	1,A1
	MOVE	A1,*A8(OZVEL),L
	CALLA	SHVELCPY
CANBNCE1
	SLEEP	1
	CALLA	CKZAMIN
***	CALLA	CKZADJST
	MOVE	*A8(OYVEL),A3,L		;GET THE CURRENT Y VELOCITY
	JRLE	CANBNCE2		;BR = NOT GOING DOWN, DON'T CHECK GROUND HIT
	CALLA	DFRMGRND		;DID HE HIT THE GROUND?
	JRLE	CANHIT			;BR = YES

CANBNCE2
	MOVE	*A8(OYVEL),A0,L		;DECREASE Y VELOCITY
	ADDI	GRAVITY,A0
	MOVE	A0,*A8(OYVEL),L
	JRUC	CANBNCE1

CANHIT1
	CALLA	PSTOP			;STOP THE SUCKER   
	MOVE	A8,A0			;OBJECT IN A0 FOR SETZPOS
	CALLA	SETZPOS			;ADJUST THE MUTHA
	CALLA	DELSHAD			;RESET SHADOW
	MOVE	A8,A0
	MOVK	1,A1 			;PUT HIM BACK IN BACKGROUND JIVE
	CALLA	ADDBB
	CLR	A0
	MOVE	A0,*A8(OPLINK),L	;DE-LINK PORFAVOR
	DIE

**************************************************************************
*                                                                        *
* CANEXCOL - COLLISION OF A BACK/FOREGROUND OBJECT AND AN EXPLOSION	 *
* A0 = PTR TO BACK/FOREGROUND OBJECT					 *
* A8 = PTR TO THE 'TING THAT SMASHED IT MAAHN				 *
*                                                                        *
**************************************************************************
CANEXCOL
	MOVI	0BDH,A1
	MOVI	CANAWAY,A7
	JRUC	BGNDGCOL
**************************************************************************
*                                                                        *
* CANRKCOL - COLLISION OF A BACK/FOREGROUND OBJECT AND A ROCKET		 *
* A0 = PTR TO BACK/FOREGROUND OBJECT					 *
* A8 = PTR TO THE 'TING THAT SMASHED IT MAAHN				 *
*                                                                        *
**************************************************************************
CANRKCOL
	MOVI	0BDH,A1
	MOVI	CANRAWAY,A7
	JRUC	BGNDGCOL
**************************************************************************
*                                                                        *
* CANCRCOL - COLLISION OF A MOVABLE BACK/FOREGROUND OBJECT AND A CAR.	 *
* A0 = PTR TO BACK/FOREGROUND OBJECT					 *
* A8 = PTR TO THE 'TING THAT SMASHED IT MAAHN				 *
*                                                                        *
**************************************************************************
CANCRCOL
	MOVI	0BDH,A1
	MOVI	CANCAWAY,A7
	JRUC	BGNDGCOL
**************************************************************************
*                                                                        *
* CANSTCOL - COLLISION OF A MOVABLE BACK/FOREGROUND OBJECT THE PLAYER	 *
*	     START CAR.							 *
* A0 = PTR TO BACK/FOREGROUND OBJECT					 *
* A8 = PTR TO THE 'TING THAT SMASHED IT MAAHN				 *
*                                                                        *
**************************************************************************
CANSTCOL
	MOVI	0BDH,A1
	MOVI	CANSAWAY,A7
	JRUC	BGNDGCOL
**************************************************************************
*                                                                        *
* DMPCRCOL - COLLISION OF A DUMPSTER OBJECT AND A CAR.	 		 *
* A0 = PTR TO DUMPSTER							 *
* A8 = PTR TO CAR THAT SMASHED IT					 *
*                                                                        *
**************************************************************************
DMPCRCOL
	MOVI	0BDH,A1
	MOVI	DMPCAWAY,A7
	JRUC	BGNDGCOL
**************************************************************************
*                                                                        *
* BGNDGCOL - GENERIC ENTRY FOR BACK/FOREGROUND OBJECT COLLISION	 	 *
* A0 = PTR TO BACK/FOREGROUND OBJECT					 *
* A1 = I.D. OF PROCESS TO CREATE					 *
* A7 = ADDRESS OF PROCESS						 *
* A8 = PTR TO THE 'TING THAT SMASHED IT MAAHN				 *
*                                                                        *
**************************************************************************
BGNDGCOL
	MOVE	A8,A10
	MOVE	A0,A8
	MOVE	*A8(OPLINK),A0,L
	JRZ	BGNDGNP
	CALLA	KILL
BGNDGNP
	CALLA	GETPRC
	MOVE	A0,*A8(OPLINK),L
	CLR	A0	
	INC	A0			;STOP THIS SCAN
	CLRC
	RETS

*
*BACKGROUND OBJECT COLLISION LIST
CANCOLL
	.WORD	B1OID|EXPLODID
	.LONG	CANEXCOL
	.WORD	B2OID|EXPLODID
	.LONG	CANEXCOL
	.WORD	BEOID|EXPLODID
	.LONG	CANEXCOL
	.WORD	B1OID|ROCKETID
	.LONG	CANRKCOL
	.WORD	B2OID|ROCKETID
	.LONG	CANRKCOL
	.WORD	C1OID|CBODYID
	.LONG	CANSTCOL
	.WORD	TRAN1OID|CBODYID
	.LONG	CANCRCOL
	.WORD	CSTR1OID|CBODYID
	.LONG	CANSTCOL
	.WORD	TRAN2OID|CBODYID
	.LONG	CANCRCOL
	.WORD	TRAN2OID|WSHLDID
	.LONG	CANSTCOL
	.WORD	0
	.LONG	DUMRTSG

DUMPCOLL
	.WORD	B1OID|EXPLODID
	.LONG	CANEXCOL
	.WORD	B2OID|EXPLODID
	.LONG	CANEXCOL
	.WORD	BEOID|EXPLODID
	.LONG	CANEXCOL
	.WORD	B1OID|ROCKETID
	.LONG	CANRKCOL
	.WORD	B2OID|ROCKETID
	.LONG	CANRKCOL
	.WORD	C1OID|CBODYID
	.LONG	CANSTCOL
	.WORD	TRAN1OID|CBODYID
	.LONG	DMPCRCOL
	.WORD	CSTR1OID|CBODYID
	.LONG	CANSTCOL
	.WORD	TRAN2OID|CBODYID
	.LONG	CANSTCOL
	.WORD	TRAN2OID|WSHLDID
	.LONG	CANSTCOL
	.WORD	0
	.LONG	DUMRTSG

	.END
